plugins {
    id "org.springframework.boot" version "2.3.10.RELEASE" apply false
}

// 配置所有项目
allprojects {
    apply plugin: 'java-library'
    apply plugin: 'io.spring.dependency-management'
    apply from: "$rootDir/version.gradle"

    // 指定jdk版本
    sourceCompatibility = 1.8
    targetCompatibility = 1.8

    group = 'com.glacier'
    description = 'eboot-platform'

    // 指定仓库
    repositories {
        mavenLocal()
        maven { url 'https://maven.aliyun.com/repository/public' }
        maven { url 'https://maven.aliyun.com/repository/central' }
        maven { url 'https://maven.aliyun.com/repository/google' }
        maven { url 'https://maven.aliyun.com/repository/spring' }
        maven { url 'https://maven.aliyun.com/repository/spring-plugin' }
        maven { url 'https://maven.aliyun.com/repository/gradle-plugin' }
        mavenCentral()
    }

    configurations.all {
        exclude group: 'org.springframework.boot', module: 'spring-boot-starter-logging'
        exclude module: 'logback-classic'
        exclude module: 'logback-core'
    }

    dependencies {
        implementation("${dependency.mapstruct}")
        annotationProcessor("${dependency.mapstructProcessor}")
        // If you are using mapstruct in test code
        testAnnotationProcessor("${dependency.mapstructProcessor}")

        testImplementation('org.springframework.boot:spring-boot-starter-test') {
            exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
        }
    }

    // 设置编码为UTF-8
    tasks.withType(JavaCompile) {
        options.encoding = "UTF-8"
    }

    javadoc {
        options {
            encoding "UTF-8"
            charSet 'UTF-8'
            author true
            version true
        }
    }

    //配置 删除文件夹
    task cleanTempFiles {
        doLast {
            delete "out"
            delete "logs"
        }
    }
    clean.dependsOn cleanTempFiles

    // 配置 创建源文件夹
    task mkdirs {
        sourceSets*.java.srcDirs*.each {
            it.mkdirs()
        }
        sourceSets*.resources.srcDirs*.each {
            it.mkdirs()
        }
    }
    // 拷贝jar包
    task copyJars(type: Copy) {
        // 运行时
        from configurations.runtimeClasspath
        // 目标位置
        into "$buildDir/libs"
    }

    test {
        useJUnitPlatform()
    }

    dependencyManagement {
        // 导入依赖管理
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:${springBootVersion}"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
            mavenBom "com.alibaba.cloud:spring-cloud-alibaba-dependencies:${springCloudAlibabaVersion}"
        }
    }
}
